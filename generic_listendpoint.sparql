#+ endpoint_in_url: False
#+ pagination: 50
#+ transform: {
#+     "label": "?entityLabel",
#+     "uri": "?entity",
#+     "type": "?entityTypeLabel",
#+     "$anchor": "uri"
#+   }

PREFIX bds:  <http://www.bigdata.com/rdf/search#>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX bioc: <http://www.ldf.fi/schema/bioc/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX idm: <https://www.intavia.org/idm/>
PREFIX : <http://www.researchspace.org/resource/>


SELECT ?entity ?entityTypeLabel ?entityLabel
WHERE {
	?entity crm:P1_is_identified_by ?appellation .
  	?entity a ?entityType .
  	?appellation a crm:E33_E41_Linguistic_Appellation ;
                	rdfs:label ?entityLabel .
  SERVICE <http://www.bigdata.com/rdf/search#search> { 
        ?entityLabel bds:search ?_q
    }
  BIND (IF(?entityType = <https://www.intavia.org/idm-core/Person_Proxy>, "Person",
      IF(?entityType = <https://www.intavia.org/idm-core/Group>, "Institution",
        IF(?entityType = <http://www.cidoc-crm.org/cidoc-crm/E53_Place>, "Place", "Entity"))) AS ?entityTypeLabel)
} 
