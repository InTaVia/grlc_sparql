#+ endpoint_in_url: False
#+ pagination: 50
#+ transform: {
#+     "label": "?personLabel",
#+     "uri": "?person",
#+     "occupation": "?occupationvalue",
#+     "$anchor": "uri"
#+   }

PREFIX bds:  <http://www.bigdata.com/rdf/search#>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX bioc: <http://www.ldf.fi/schema/bioc/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?person ?surnameLabel ?forenameLabel ?occupationvalue
WHERE {
    ?person a crm:E21_Person .
    ?person crm:P1_is_identified_by ?appellation .
    ?appellation a crm:E33_E41_Linguistic_Appellation ;
                   crm:P148_has_component ?surname ;
                   crm:P148_has_component ?forename .
    ?surname crm:P2_has_type <https://www.intavia.org/nametype/surname> .
    ?forename crm:P2_has_type <https://www.intavia.org/nametype/forename> .
    ?surname rdfs:value ?surnameLabel .
    ?forename rdfs:value ?forenameLabel .
    ?person bioc:has_occupation ?occupation .
    ?occupation rdfs:label ?occupationvalue
    SERVICE <http://www.bigdata.com/rdf/search#search> { 
        ?occupationvalue bds:search ?_occupation_de
    }
}
ORDER BY DESC(?person)